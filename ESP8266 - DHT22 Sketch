

#include <ESP8266WiFi.h>
#include <MySQL_Connection.h>
#include <MySQL_Cursor.h>
#include <WiFiClient.h>
#include "DHTStable.h"

//Connect to Wifi Network
const char* ssid     = "//WIFI NETWORK";
const char* password = "//WIFI PASSWORD";
WiFiClient client;
// MySQL server details
IPAddress server_addr(0, 0, 0, 0); // IP of the MySQL server here
char user[] = "USERNAME"; // MySQL user login username
char pass[] = "PASSWORD"; // MySQL user login password

//Query to insert readings into MySQL, update value 1 to sensor id
char INSERT_SQL_FORMAT[] = "INSERT INTO BabyMonitor.SensorReadings (Sensor_Id, Reading) VALUES (2, %f)";
char query[128];
MySQL_Connection conn((Client *)&client);

DHTStable DHT;

// global variables
const char host[] = "Lounge"; // update for each sensor
const int dataPin = 5;

void setup() {
  // start serial communication at 9600 bits per second:
  Serial.begin(9600);
  Serial.setTimeout(2000);

  // Wait for serial to initialize.
  while (!Serial) { }

  Serial.println("Device Started");
  Serial.println("-------------------------------------");

  // connect to WiFi
  WiFi.hostname(host);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");

  }
  delay(2000);

  // open connection to MySQL DB
  while (conn.connect(server_addr, 3306, user, pass) != true) {
    delay(500);
    Serial.print ( "." );
  }
}

void loop() {
  // Read temperature
  Serial.println("Getting reading ");
  int ReadData = DHT.read22(dataPin);
  float t = DHT.getTemperature();
  Serial.println(t);
  Serial.println("-------------------------------------");

  // Create cursor to execute SQL query
  MySQL_Cursor *cur_mem = new MySQL_Cursor(&conn);
  // Parse query to insert into SQL Table
  sprintf(query, INSERT_SQL_FORMAT, t);
  // Execute the query
  cur_mem->execute(query);
  // Delete the cursor
  delete cur_mem;

  // run again in 1 hour
  delay(60 * 60 * 1000UL);

}
